// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/Memory.hdl
/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load=1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the Screen
 * and Keyboard chip specifications given in the lectures and the book.
 */
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:

    // See pattern in addresses - bit 13-14 change from 00,01,10,11 just like Mux/DMux behaviour
    DMux4Way(in=load, sel=address[13..14], a=wLoadRAM0, b=wLoadRAM1, c=wLoadScreen, d=wLoadInvalid);
    Or(a=wLoadRAM0, b=wLoadRAM1, out=wLoadRAM);

    // This needs to become output if address = 0x0000-0x3FFF = 0b000000000000000 - 0b011111111111111
    RAM16K(in=in, load=wLoadRAM, address=address[0..13], out=wOutRAM);

    // This needs to become output if address = 0x4000-0x5FFF = 0b100000000000000 - 0b101111111111111
    Screen(in=in, load=wLoadScreen, address=address[0..12], out=wOutScreen);

    // This needs to become output if adress = 0x6000 = 0b110000000000000
    Keyboard(out=wOutKeyboard);

    // Same as DMux above
    Mux4Way16(a=wOutRAM, b=wOutRAM, c=wOutScreen, d=wOutKeyboard, sel=address[13..14], out=out);

}